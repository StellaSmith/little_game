cmake_minimum_required(VERSION 3.16)
project(vengine_tools)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

function(add_simple_tool tool_name)
    add_executable(${tool_name})
    target_sources(${tool_name} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/src/${tool_name}.cpp)
    target_compile_features(${tool_name} PRIVATE cxx_std_20)

    foreach(dep ${ARGN})
        if(dep MATCHES "^([a-zA-Z0-9]+)\/(.+)$")
            set(package "${CMAKE_MATCH_1}")
            set(target "${CMAKE_MATCH_2}")
        elseif(dep MATCHES "^([a-zA-Z0-9]+)::([a-zA-Z0-9]+)$")
            set(package "${CMAKE_MATCH_1}")
            set(target "${CMAKE_MATCH_0}")
        else()
            message(FATAL_ERROR "invalid dependecy string: ${dep}")
        endif()
        find_package(${package} REQUIRED)
        target_link_libraries(${tool_name} PRIVATE ${target})
    endforeach()
    install(TARGETS ${tool_name} RUNTIME DESTINATION bin)
endfunction()

add_simple_tool(resource_compiler Boost/boost::boost argparse::argparse fmt::fmt RapidJSON/rapidjson)
add_simple_tool(install_shared_libraries argparse::argparse fmt::fmt ctre::ctre)